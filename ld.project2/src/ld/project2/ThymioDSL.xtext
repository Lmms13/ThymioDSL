grammar ld.project2.ThymioDSL with org.eclipse.xtext.common.Terminals

generate thymioDSL "http://www.project2.ld/ThymioDSL"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model returns Model:
	(procedures+=Procedure)+
;
	
Procedure returns Procedure:
	'Procedure:'
	name = EString
	'Event:'
	(events = Event)
	'Actions:'
	(actions+=Action)+
;

Event returns Event:
	('button_is_clicked:'
	(button = Ortogonal))|
	('robot_is_tapped:'
	(tap = EBoolean))|
	('sound_is_detected:'
	(mic = EBoolean))|
	('sensor_is_activated:'
	(sensor+= (Sensor)))+
;

Sensor returns Sensor:
	('back_left:'
	(backLeftSensor = BlackWhite))|
	('back_right:'
	(backRightSensor = BlackWhite))|
	('front_right:'
	(frontRightSensor = EBoolean))|
	('front_center_right:'
	(frontCenterRightSensor = EBoolean))|
	('front_center:'
	(frontCenterSensor = EBoolean))|
	('front_center_left:'
	(frontCenterLeftSensor = EBoolean))|
	('front_left:'
	(frontLeftSensor = EBoolean))
;

Action returns Action:
	('move:'
	(move = (Motors)))|
	('lights:'
	(light = (Lights)))|
	('sound:'
	(sound+= (Sound)+))
;

Motors returns Motors:
	'left_motor:'
	(left = Addition)
	'right_motor:'
	(right = Addition)
;

Lights returns Lights:
	{Lights} ('top_light:'
	(('on'
	(topLight = (RGB)))|
	('off')))
	('bottom_light:'
	(('on'
	(bottomLight = (RGB)))|
	('off')))
;

RGB returns RGB:
	'('
	(red = Addition)
	','
	(green = Addition)
	','
	(blue = Addition)
	')'
;

Sound returns Sound:
	'note:'
	(pitch = Addition)
	(duration = NoteDuration)
;

EInt returns ecore::EInt:
	'-'? INT;

EString returns ecore::EString:
	STRING | ID;
	
EBoolean returns ecore::EBoolean:
	'true' | 'false';
	
NoteDuration returns ecore::EString:
	'short' | 'long';

Ortogonal returns ecore::EString:
	'up' | 'down' | 'left' | 'right' | 'center';	
	
BlackWhite returns ecore::EString:
	'black' | 'white';
	
Addition returns Expression:
  Multiplication ({Addition.left=current} operator=('+'|'-') right=Multiplication)*;

Multiplication returns Expression:
  Number ({Multiplication.left=current} operator=('*'|'/') right=Number)*;

Number returns Expression:
  value = EInt |
  '(' Addition ')';